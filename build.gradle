plugins {
    id 'nebula.java-cross-compile' version '0.10.0' apply false
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'me.tatarka.gradle.pom' version '1.0' apply false
}

def rootBuildDir = buildDir

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'nebula.java-cross-compile'
    apply plugin: 'maven-publish'

    group = 'org.javacord'
    version = '3.0.0-SNAPSHOT'
    description = 'A multithreaded but easy to use Discord Java API'
    buildDir = rootProject.file("${rootBuildDir}/${name}")

    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
        options.incremental true
    }
}

clean {
    delete rootBuildDir
}

subprojects {
    ext.shortName = name - ~/^javacord-/
    description = "A multithreaded but easy to use Discord Java API ($shortName)"
}

// remove non-sense task from com.github.johnrengelman.shadow plugin
tasks.remove knows

defaultTasks 'build'

apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/jars.gradle'
apply from: 'gradle/java9.gradle'
apply from: 'gradle/javadoc.gradle'
apply from: 'gradle/pom.gradle'
apply from: 'gradle/publishing.gradle'
