plugins {
    id 'nebula.java-cross-compile' version '0.10.0' apply false
    id 'com.github.johnrengelman.shadow' version '2.0.2' apply false
    id 'me.tatarka.gradle.pom' version '1.0' apply false
    id 'net.researchgate.release' version '2.7.0' apply false
}

if (project.hasProperty('teamcity')) {
    println "##teamcity[buildNumber '$version (#${project.'build.number'})']"
}

def rootBuildDir = buildDir

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'nebula.java-cross-compile'

    group = 'org.javacord'
    description = 'A multithreaded but easy to use Discord Java API'
    buildDir = rootProject.file("${rootBuildDir}/${name}")

    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
        options.incremental true
    }
}

clean {
    delete rootBuildDir
}

subprojects {
    ext.shortName = name - ~/^javacord-/
    description = "A multithreaded but easy to use Discord Java API ($shortName)"
}

defaultTasks 'build'

apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/jars.gradle'
apply from: 'gradle/java9.gradle'
apply from: 'gradle/javadoc.gradle'
apply from: 'gradle/pom.gradle'
apply from: 'gradle/publishing.gradle'
